java {
    withJavadocJar()
    withSourcesJar()
}

def isSnapshot = project.getVersion().endsWith('SNAPSHOT')
def repoUrl = isSnapshot ? 'https://central.sonatype.com/repository/maven-snapshots/'
        : "https://ossrh-staging-api.central.sonatype.com/service/local/"
def user = project.hasProperty('sonatypeUsername') ? project.property('sonatypeUsername') : System.getenv('SONATYPE_USERNAME')
def pass = project.hasProperty('sonatypePassword') ? project.property('sonatypePassword') : System.getenv('SONATYPE_PASSWORD')

if (!isSnapshot) {
    publish.finalizedBy(rootProject.getTasks().getByName('closeAndReleaseRepository'))
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'ModuleFS'
                description = 'The ModuleFS library provides a simple file system implementation to access the contents of Java modules in a unified way.'
                url = 'https://github.com/xpipe-io/modulefs'
                licenses {
                    license {
                        name = 'The MIT License (MIT)'
                        url = 'https://github.com/xpipe-io/modulefs/LICENSE.md'
                    }
                }
                developers {
                    developer {
                        id = 'crschnick'
                        name = 'Christopher Schnick'
                        email = 'crschnick@xpipe.io'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/xpipe-io/modulefs.git'
                    developerConnection = 'scm:git:ssh://github.com/xpipe-io/modulefs.git'
                    url = 'https://github.com/xpipe-io/modulefs'
                }
            }
        }
    }

    repositories {
        maven {
            setUrl repoUrl
            credentials {
                setUsername user
                setPassword pass
            }
        }
    }
}

def signingKeyId = project.hasProperty('signingKeyId') ? project.property("signingKeyId") : System.getenv('GPG_KEY_ID')
def signingKey = project.hasProperty('signingKeyFile') ? file(project.property("signingKeyFile")).text : System.getenv('GPG_KEY')
def signingPassword = project.hasProperty('signingPassword') ? project.property("signingPassword") : System.getenv('GPG_KEY_PASSWORD')

signing {
    useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword != null ? signingPassword : '')

    sign publishing.publications.mavenJava
}

nexusStaging {
    serverUrl = "https://ossrh-staging-api.central.sonatype.com/service/local/"
    packageGroup = "io.xpipe"
    username = user
    password = pass
}